plugins {
    id 'org.ajoberstar.grgit' version '4.0.2' apply false
}
import org.ajoberstar.grgit.Grgit

rootProject.name = 'androidtest'

def msgPrefix = rootProject.name + " settings: "

def compositeLibraries = ["math", "craco"]
for (String lib : compositeLibraries) {
    def libPrefix = "dependency " + lib
    def libPath = file("../" + lib).getPath()
    def grgitLib
    if (file(libPath).exists()) {
        println(msgPrefix + libPrefix + ": Local repository exists.")
        grgitLib = Grgit.open(dir: libPath)
    } else {
        println(msgPrefix + libPrefix + ": Local repository does not exist. Cloning to " + libPath + ".")
        grgitLib = Grgit.clone(dir: libPath, uri: "https://github.com/cryptimeleon/" + lib + ".git")
    }
    def branchThis = grgitLib.branch.current()
    println(msgPrefix + libPrefix + ": Enabling composite build using branch " + branchThis.getName())
    includeBuild(libPath)
}